plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dev.rikka.tools.hidden-api-refine'
}
hiddenApiRefine {
    // Prefix to remove, default is ['$']
    refinePrefix = ['$']
    // Print log, default is true
    log = true
}

int incrementRefine() {
    Properties properties = new Properties()
    String fileName = "custom.properties"
    def key = "build.refine"
    File propertyFile = new File(rootDir.getAbsoluteFile(), fileName)
    properties.load(propertyFile.newDataInputStream())
    def refine = properties.getProperty(key).toInteger() + 1
    properties.setProperty(key, refine.toString())
    properties.store(propertyFile.newDataOutputStream(), 'Increment this value on every single build. ' +
            'You can access this value by calling BuildConfig.REFINE')
    return refine
}

def refine = incrementRefine()
android {
    signingConfigs {
        sign
    }
    compileSdkVersion 30

    defaultConfig {
        applicationId "top.xjunz.automator"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "1.0.1"+ ".r" + refine
        if(gradle.startParameter.taskNames.any{it.toLowerCase().contains("debug")}){
            versionName+="-debug"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "int", "REFINE", refine.toString()
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            debuggable true
            signingConfig signingConfigs.sign
        }
    }
    buildFeatures {
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(":automator")
    compileOnly project(":automator:hidden-apis")
    def shizuku_version = '11.0.3'
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    // For using AppIconLoader directly.
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
apply from: rootProject.file("sign.gradle")